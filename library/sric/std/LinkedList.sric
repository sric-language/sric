
abstract struct Linkable$<T> {
    var next: own*? T;
    var previous: *? T;
}

/**
* LinkedList is a container that supports constant time insertion and removal of elements from anywhere in the container. Fast random access is not supported. It is usually implemented as a doubly-linked list. 
*/
struct LinkedList$<T: Linkable$<T>> {
    private var head: own*? T = null;
    private var tail: *? T = null;
    private var length: Int = 0;

    fun clear() {
        head = null;
        tail = null;
        length = 0;
    }

    fun size() const : Int {
        return length;
    }

    fun add(elem: own* T) {
        if (tail == null) {
            insert(move elem);
            return;
        }

        elem.next = null;
        elem.previous = tail;
        tail.next = move elem;
        tail = tail.next;
        ++length;
    }

    fun insert(elem: own* T) {
        elem.next = move head;
        elem.previous = null;

        if (elem.next != null) {
            elem.next.previous = elem;
        }
        head = move elem;
        if (tail == null) {
            tail = head;
        }
        ++length;
    }

    fun insertBefore(elem: own* T, pos: * T) {
        if (pos.previous == null) {
            insert(move elem);
            return;
        }
        elem.next = move pos.previous.next;
        elem.previous = pos.previous;
        pos.previous = elem;
        elem.previous.next = move elem;
        ++length;
    }

    fun remove(elem: *? T) : Bool {
        if (elem == null) return false;
        var next = move elem.next;
        var previous = elem.previous;
        elem.previous = null;
        
        if (next != null) {
            next.previous = previous;
        }

        if (head == elem) {
            head = move next;
        }
        if (previous != null) {
            previous.next = move next;
        }

        if (tail == elem) {
            tail = previous;
        }

        --length;
        return true;
    }

    fun isEmpty() const : Bool {
        return length == 0;
    }

    fun constFirst() const : *? const T {
        return head;
    }

    fun first(): *?T {
        return head;
    }

    fun last(): *?T {
        return tail;
    }

    fun end(): *?T {
        return null;
    }
}